
# ========== #
# Delimiters #
# ========== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;
SOFT-DELIMITERS = "<,>" ;

# ============= #
# Tags and sets #
# ============= #

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

# First level tags
# ----------------

LIST A = adj ;
LIST Adv = adv ;
LIST Pron = prn ;
LIST N = n ;
LIST Prop = np ;
LIST V = v ;
LIST Vaux = vaux ;
LIST Cop = cop ;
LIST Det = det ;
LIST CC = cnjcoo ;
LIST CS = cnjsub ;
LIST Interj = ij ;
LIST Num = num ;
LIST Post = post ;
LIST Postadv = postadv ;
LIST Cm = cm ;

# POS sub-categories
# ------------------

LIST Pers = pers ;
LIST Interr = itg ;

# "Syntactic" tags
# ----------------

LIST Advl = advl ;
LIST Attr = attr ;
LIST Subst = subst ;

# Morphosyntactic properties
# --------------------------

LIST Sg = sg ;
LIST Pl = pl ;

LIST Nom = nom ;
LIST Dat = dat ;
LIST Acc = acc ;
LIST Abl = abl ;
LIST Loc = loc ;

LIST P3 = p3 ;

# Semantic lists
# --------------

LIST MOVEMENT-V = "жүгір" ;

LIST MOVEMENT-ADV = "тиз" "акрын" "әкрен" ;

# All possible word categories
# ----------------------------

SET WORD = N | V | A | Post | Postadv | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;

SET PRE-N =  A | Det | Postadv | Num | (n gen) | (prn gen) | CC ;

SET MARK =  Cm | ("\\") | ("\;") | ("\(") ;

SET WORDMARK = WORD | MARK ;

SET N-MOD = A | Det | Num | (n gen) | (prn gen) ;

SET ADJ-MOD = Postadv | Adv ;

SET NPMARK = N-MOD | ADJ-MOD ;

# Categories which cannot be part of a noun phrase
# ------------------------------------------------

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;

# Noun sets
# ---------

LIST N-SG-NOM = (n sg nom) ;
LIST N-PL-NOM = (n pl nom) ;

# Pronoun sets
# ------------

SET PRN-PERS = (prn pers) ;

# Adjective sets
# --------------

LIST A/Advl = (adj advl) ;

LIST A/Subst = (adj subst nom) ;

SET A1 = A/Advl | A/Subst ;

# Verb sets
# ---------

LIST V-P3 = (v p3) (vaux p3) (cop p3) ;

LIST Gerund = ger ger_ppot ger_past ger_perf ger_impf ger_abs ;

LIST Participle = prc_perf prc_impf prc_cond prc_vol ;

SET V-NotGerund = V - Gerund ;

LIST Copula = (cop p3 sg) (cop p3 pl) ;

LIST FourAuxilliaries = "тұр" "жүр" "отыр" "жатыр" ;

# Barriers
# --------

SET S-BOUNDARY = CS | Interr | EOS ;


# ======= #
  SECTION
# ======= #

# some of the rules having nouns as constraints didn't work when the noun has
# personal copulas as subreadings, so the rule removing personal copulas is
# placed into a separate section, before any other rules

#
 REMOVE Copula IF
        (NOT 1 EOS OR MARK)
;
#

# АРАЛ ТЕҢІЗІ.


# ======= #
  SECTION
# ======= #


SELECT Adv IF (0 MOVEMENT-ADV) (0 A OR Adv) (1* MOVEMENT-V BARRIER S-BOUNDARY);



# Determiners
# ===========



"<бір>" SELECT Det IF (-1C A) (1 N);
    #! Әхмәт тиз генә иске зур _бер_ агачка йөгерә.
    #! Әхмәт акрын гына иске зур _бер_ агачка йөгерә.

"<бар>" SELECT V IF (-1 Dat) ;
"<бар>" SELECT A IF (-1 (px3sp)) ;
"<бар>" SELECT Det IF (1* NPMARK BARRIER N OR MARK) ;
# 172. Сот арқылы немесе өзгеде осындай құзыреті _бар_ мемлекеттік органдардың
# шешімі арқылы тәркіленген мұражайлық маңызы бар жинақтардың немесе жекелеген заттардың есебінен;
#! Бар<det> җирдән карый, әмма Әхмәтне таба алмый.
#! Акча бармы? Бар<adj>.
#! Акча бармы? Бар<adj>, ләкин бик аз!



# Nouns
# =====



 REMOVE Attr IF
       (0 N OR Prop)
       (NOT 0 Loc)
       (NOT 1 N OR Prop)
;
# 176. Орталықта Түркістанның төл тарихына арналған музейлік [0]экспозиция жасақталған.


# Select attributive reading of the first noun of the II izafet construct
# (as Tatar grammars call it)
 SELECT Attr IF
        (0C N OR Prop)
        (1 N OR Prop OR Subst)
        (1 (px3sp))
;
# 111. Түркияның оңтүстік-шығысындағы [0]Газиантеп қаласында кеше болған жарылыстан қаза тапқандар саны 9-ға жетті.


# If a former gerund or verbal adjective was lexicalized as a noun, select noun
 SELECT N IF
        (0 N)
        (0 Gerund  OR (gpr_pot))
;
# professions, such as "оқушы"; -U gerunds , in theory other gerunds too



# Adjectives
# ==========



# select adverbial reading of adjectives if any verbal form except gerund follows
 SELECT Advl IF
         (1C V-NotGerund)
;
#

# select pure adjective reading ("predicative" as it is called) of adjective if sentence ends
 REMOVE Advl IF
        (0 A1)
        (1 EOS)
;
 REMOVE Subst IF
         (0 A1)
         (1 EOS)
;
#



# Verbs
# =====



#
 REMOVE Participle IF
        (NOT 1C V OR Vaux)
;
#*

# [0] is auxilliary, because a participle preceedes, and [0] is not in imperative,
# because there is a subject (i.e. something in nominative closest to the
# beginning of the sentence)
 SELECT (vaux p3) IF
        (0 FourAuxilliaries)
        (-1 (prc_perf))
        (@1 Nom)
;
 SELECT (vaux p3) IF
        (0 FourAuxilliaries)
        (-1 (prc_perf))
        (-1* (prn nom) BARRIER BOS)
;
#* 15. Олардың анасы мысығымен бірге үйде, ол терезеден Азамат пен Айгүлдің ойнағанына қарап [0]тұр.


# imperative form is a bare stem, so it is often ambigious with other pars of speech
 REMOVE (imp) IF
        (NOT 1 EOS OR MARK OR ("де"))
##  96 . Ал оң жақ жағалауы батпақты, онда[0] қалың орман өскен.
## Бар деп айттым мен оған.

# Wordform specific rules   # Adding new stuff here is discouraged.
# =======================   # Try to generalize, if possible, existing rules.



 "<өте>" SELECT Adv IF
                (1 A)
;
#! Бірақ кеше _өте_ суық еді!


 "<кейін>" SELECT Post IF
                (-1 Abl)
;
#! 23. Айгүл санап біткеннен кейін айналасына қарады.


# ugly and RISKY
 "<сол>" SELECT Det IF
        (NOT 1 ("қол"i) OR ("жақ"i))
;
#


#:
 "<аса>" SELECT Adv IF
        (1 A)
;
# "Әл-Истахри келтірген мәліметтер аса құнды саналады."


#
 "<ал>" REMOVE CC IF
        (NOT -1 BOS OR Cm )
;
# Бөлмеде шкаф бар, содан бiр кiтап ал.


#
 "<ешкім>" SELECT Pron IF
                  (1* ("жоқ"i) OR (v neg) BARRIER EOS)
;
#  70. Оның рекордын әзірше ешкім бұза алған жоқ.

#
 "<қарай>" SELECT Post IF
        (1* MOVEMENT-V)
;
#


#
 "<жылы>" SELECT N IF
        (-1 Num)
;
#


#
 "<қарасты>" SELECT A IF
        (-1 (dat))
;
# 177. Орталыққа _қарасты_ Түркістан қаласында Н.Оңдасынов атындағы
# тарихи-мемориалдық мұражайы атты филиал жұмыс жасайды.


#
 "<атты>" REMOVE V IF
        (NOT 1 EOS OR Cm)
;
#


 "<қайта>" SELECT Adv IF
        (1 ("баста"i))
;
#


#
 "<Енді>" SELECT Adv IF
        (-1 BOS)
;
#


#
 "<көптеген>" SELECT Det IF
        (1 N + Pl)
;
#

# ambigious with v.pass
 "<құрал>" SELECT N IF
        (-1 A OR (n attr))
;
#



# UGLY
# "сый" is certainly not <v.iv.imp> in the following example,
# I don't think that сый<imp> is ever used
 "<сый>" SELECT N
;
# 171. Жеке және заңды тұлғалардан _сый_ немесе мұра ретінде қабылданған заттар есебінен;



# imperative of "ісу" ("to swell") - just like "<сый>" above sounds odd,
# but let's try to deal with it in a less ugly way
 "<іс>" SELECT N IF
        (-1 A)
;
# 178. ...тәрбиелік мәні бар іс-шараларды бірге ұйымдастыру...


# FIXME: could be achieved with a more general rule I think
 "<көрме>" SELECT N IF
        (1 ("зал"i))
;
# 181. Ғимарат 3 қабаттан, 5 көрме залынан, конференция залынан, қолөнершілердің шеберханасы...



###################   TEMPORARY HACKS TO GET THE STORY TRANSLATED   ####################
###################   SOME SHOULD BE REWRITTEN LATER                ####################


REMOVE N IF (0 ("ақырын"));
    ## Айгүл оны _ақырын_ тыңдайды.

SELECT A/Advl IF (0 ("ақырын"));
    ## Айгүл оны _ақырын_ тыңдайды.

REMOVE N IF (0 ("жылы")) (NOT -1 N OR Num) ;
    #! Ауа райы бүгін әбден жақсы, ^жылы/жыл<n><px3sp><nom>/жылы<adj>/жылы<adj><subst><nom>.

###################   TO BE DONE   ####################

# Deciding about the number of a verb or copula in the 3 person
# Basic idea is to remove plural reading if subject is not in plural,
# but there a lot of corner cases, which lead to lots of mispredictions.
# So let's just delete plural reading for now.
#
# In Tatar, when the subject indicates the number, this LAR affix for the verbs
# in 3 person is kind of optional too, so singular reading will work/is right for
# the majority of the cases.
#
# Selecting plural reading might be more productive.
 REMOVE Pl IF
        (0 V OR Vaux OR Cop)
        (0 P3)
;

###################################### TO BE SORTED ############################


#:
 SELECT Interj IF
         (-1 BOS)
         (1 Cm)
;
#  "Мысалы, ежелгі заманның өзінде Арал теңізі көп елдерге мәлім болған."


# get rid of postposition reading if noun/pronoun/any_subst._thing/gerund doesn't precede
 REMOVE Post IF
         (NOT -1 N OR Prop OR Pron OR Subst OR Gerund)
;
#


#
 SELECT A IF
        (0 ("құрама"))
        (-1 (np top))
;
# 69. Франция құрамасының бұрынғы ойыншысы Мишель Платини...

# FIXME: I wanted to refer "жәдігер" as a lemma, but it didn't work
# (because it has cop p3 subreding I guess)
 "<жинағы>" SELECT N IF
        (-1 ("<жәдігерлер>"))
;
# 165. ...тарихи жәдігерлер жинағы арқылы көрсету.

#
 SELECT N IF
        (0 ("қор"))
        (-1 N)
;
# 169. Музей қоры Қазақстан Республикасы Мәдениет және ақпарат министрінің...

# if a word is ambigious between verbal adjective and gerund (in nominative - if there is a case affix it's a bit
# complicated since the verbal adjective can be substantivized), select verbal adverb reading if a noun follows
# SELECT V-ADJECTIVE IF
#        (0 Gerund (nom))
#        (0 V-ADJECTIVE)
#        (1 N)
#;


# RISKY
#REMOVE Acc IF (0 ("қызықты")) (NOT 1 V);
 REMOVE Acc IF
        (0 (n acc) OR (adj subst acc))
        (0 A)
        (NOT 1 V)
;
#! Еуро-2012 ^туралы/туралы<post>$ ^қызықты/қызық<adj><subst><acc>/қызықты<adj>/қызықты<adj><advl>/қызықты<adj><subst><nom>$ ^статистикаға/статистика<n><dat>$ көз салыңыз.

# FIXME CHECK
# the three ambiguities above are quite common - an adjective (A4) with -LI is
# ambiguous with: 1) a noun or adjective in accusative, and 2) with  a verb in
# <ifi> form. So it is a good idea to have a more general rule:
 REMOVE V IF
        (0 A)
        (0 (ifi))
        (NOT 1 EOS OR Cm)
;
#

#
 SELECT Prop IF
        (0 N)
        (0 Prop)
        (0 ("[:upper:]+[:lower:]*"r))
        (NOT -1 BOS)  ;
# 108. Өйткені бүгінгі _Арал_ деген атау сол XVII ғасырдан бергі жерге берілген.

#
 SELECT Prop IF
        (0 N)
        (0 Prop)
        (0 ("[:upper:][:upper:]+"r))
# АЗАМАТ ҚАЙДА?

#
